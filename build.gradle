plugins {
  id "com.moowork.node" version "1.3.1"
}

version = version == 'unspecified' ? '0.0.0' : version

def extensionFile = "groovy-guru-${project.version}.vsix"

task build {
}

task copyJar(type: Copy) {
    from "${projectDir}/../build/libs/groovy-language-server-all.jar"
    into "${buildDir}/bin"
}

task webpack(type: Exec) {
  inputs.file("package.json")
  inputs.file("package-lock.json")
  inputs.file("webpack.config.js")
  inputs.file("tsconfig.json")
  inputs.dir("src")
  outputs.file("${buildDir}/extension.js")
  outputs.file("${buildDir}/extension.js.map")

  def webpack = "${projectDir}/node_modules/.bin/webpack"

  if (System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")) {
    webpack += ".cmd"
  }
  commandLine "$webpack", "--mode", "production"
}

task vsce(type: Exec) {
  inputs.dir(buildDir)
  outputs.file(extensionFile)

  def vsce = "${projectDir}/node_modules/.bin/vsce"

  if (System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")) {
    vsce += ".cmd"
  }

  commandLine(vsce, 'package', project.version, '--no-git-tag-version')
}

task publish(type: Exec) {
  inputs.file(extensionFile)

  def vsce = "${projectDir}/node_modules/.bin/vsce"

  if (System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows")) {
    vsce += ".cmd"
  }

  commandLine(vsce, 'publish', '--no-git-tag-version', '--packagePath', extensionFile)
}

tasks.webpack.dependsOn tasks.npmInstall
tasks.vsce.dependsOn tasks.copyJar
tasks.vsce.dependsOn tasks.webpack
tasks.build.dependsOn tasks.vsce
tasks.publish.dependsOn tasks.build

