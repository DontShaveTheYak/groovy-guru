plugins {
  id 'maven-publish'
  id "com.github.johnrengelman.shadow" version "5.2.0"
  id "java"
}

version = project.findProperty('CICD_TAG') ?: '0.0.0-DEV'

group = 'io.github.dontshavetheyak'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

test {
  useJUnitPlatform()
}

ext.mainClass = "net.prominic.groovyls.GroovyLanguageServer"

tasks.build.dependsOn tasks.shadowJar

shadowJar {
  manifest {
    attributes "Main-Class": "net.prominic.groovyls.GroovyLanguageServer"
  }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = 'groovy-butler-ls'
                description = 'A copy of the groovy-language-server.'
                url = 'https://github.com/DontShaveTheYak/groovy-butler'
                inceptionYear = '2021'

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'shadycuz'
                        name = 'Levi Blaney'
                        email = 'shadycuz@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/DontShaveTheYak/groovy-butler'
                    connection = 'scm:https://github.com/DontShaveTheYak/groovy-butler.git'
                    developerConnection = 'scm:git@github.com:DontShaveTheYak/groovy-butler.git'
                }
            }
        }
    }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/DontShaveTheYak/groovy-butler"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

repositories  {
  mavenCentral()
}

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.12.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.12.0"
  implementation "org.codehaus.groovy:groovy-all:2.5.14"
  implementation "com.google.code.gson:gson:2.8.7"
  implementation "io.github.classgraph:classgraph:4.8.108"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

// assuming that Moonshine-IDE is checked out in a folder that has the same
// parent folder as groovy-language-server, copies the built .jar file to the
// appropriate location
task deployMoonshine(type: Copy) {
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineSharedCore/src/elements/groovy-language-server/"
}
task deployMoonshineTest(type: Copy) {
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineDESKTOPevolved/bin-debug/elements/groovy-language-server/"
}
